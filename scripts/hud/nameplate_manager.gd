# nameplate_manager.gd  (Godot 4.6.3)
extends Control
class_name NameplateManager

@export var nameplate_scene: PackedScene
@export var show_within_meters: int = 30000
@export var pixel_offset_up: int = 35
@export var docking_manager_path: NodePath

const POI_UNVISITED_COLOR: Color = Color(0.2, 0.95, 1.0, 1.0)
const POI_VISITED_COLOR: Color = Color(1.0, 0.72, 0.2, 1.0)
const GATEWAY_COLOR: Color = Color(0.35, 1.0, 0.95, 1.0)

var _camera: Camera3D
var _ship: Node3D
var _docking_manager: DockingManager
var _pool: Array[Control] = []
var _map: Dictionary[Node3D, Control] = {}

func _ready() -> void:
	_resolve_docking_manager()

func init(cam: Camera3D, ship: Node3D) -> void:
	_camera = cam
	_ship = ship
	_resolve_docking_manager()

func _process(_dt: float) -> void:
	if _camera == null or nameplate_scene == null:
		return
	_sync_targets()

func _sync_targets() -> void:
	var targets: Array = get_tree().get_nodes_in_group("targets")
	var pois: Array = get_tree().get_nodes_in_group("pois")
	var gateways: Array = get_tree().get_nodes_in_group("boss_gateways")
	targets.append_array(pois)
	targets.append_array(gateways)
	
	for t in targets:
		var target: Node3D = t
		if target == null:
			continue
		if not _map.has(target):
			var ui: Control = _pool_take()
			if ui.get_parent() != self:
				if ui.get_parent() != null:
					ui.get_parent().remove_child(ui)
				add_child(ui)
			_map[target] = ui
			
			var tracked: Node3D = target
			target.tree_exited.connect(func() -> void:
				if _map.has(tracked):
					_pool_release(_map[tracked])
					_map.erase(tracked)
			)

	var vp_size: Vector2 = get_viewport().get_visible_rect().size
	for target in _map.keys():
		var ui: Control = _map[target]
		var pos3: Vector3 = target.global_position

		var ship_pos: Vector3 = _ship.global_transform.origin if (_ship != null) else _camera.global_position
		var distance_sq: float = pos3.distance_squared_to(ship_pos)
		var visible_now: bool = (
			distance_sq <= float(show_within_meters * show_within_meters)
			and _camera.is_position_in_frustum(pos3)
		)

		if visible_now:
			var d: float = sqrt(distance_sq)
			var sp: Vector2 = _camera.unproject_position(pos3)
			ui.visible = true

			var label: Label = ui.get_node("HBox/Label") as Label
			var distance_text: String = HudDistanceFormatter.format_distance(d)
			if label != null:
				var kind: String = _kind_of(target)
				_reset_poi_indicator(ui)
				if kind == "enemy":
					label.text = "%s (%s)\n%s  •  HP: %d | S: %d" % [
						target.display_name,
						target.get_faction_display_name(),
						distance_text,
						int(round(max(target.hull, 0.0))),
						int(round(max(target.shield, 0.0)))
					]
				elif kind == "target":
					label.text = "%s\n%s  •  HP: %d" % [
						target.display_name,
						distance_text,
						int(round(max(target.hull, 0.0)))
					]
				elif kind.contains("poi"):
					var type_tag: String = _get_poi_type_tag(target)
					_apply_poi_indicator(ui, target)
					label.text = "%s [%s]\n%s" % [
						target.display_name,
						type_tag,
						distance_text
					]
				elif kind == "boss_gateway":
					_apply_gateway_indicator(ui)
					label.text = "%s\n%s  •  Dock to Advance" % [
						_gateway_display_name(target),
						distance_text
					]

			_place_center_bottom(ui, sp, vp_size)
		else:
			_pool_release(ui)
			_map.erase(target)

func _pool_take() -> Control:
	if _pool.is_empty():
		return nameplate_scene.instantiate() as Control
	return _pool.pop_back()

func _pool_release(ui: Control) -> void:
	_reset_poi_indicator(ui)
	ui.visible = false
	if ui.get_parent() != null:
		ui.get_parent().remove_child(ui)
	_pool.push_back(ui)

func _place_center_bottom(ui: Control, screen_pos: Vector2, vp_size: Vector2) -> void:
	ui.size = ui.get_combined_minimum_size()
	var s: Vector2 = ui.size
	var pos: Vector2 = screen_pos - Vector2(s.x * 0.5, s.y) - Vector2(0.0, float(pixel_offset_up))
	pos.x = clamp(pos.x, 0.0, vp_size.x - s.x)
	pos.y = clamp(pos.y, 0.0, vp_size.y - s.y)
	ui.position = pos

func _kind_of(node: Object) -> String:
	if not is_instance_valid(node):
		return "unknown"
	# Check POI first (before meta) to ensure proper type detection
	if node is Enemy:
		return "enemy"
	if node is TargetObject:
		return "target"
	if node is PoiInstance:
		return "poi"
	if node is BossGateway:
		return "boss_gateway"
	if node.has_meta("kind"):
		var kind_meta: String = String(node.get_meta("kind"))
		if kind_meta != "":
			return kind_meta
	return "unknown"


func _get_poi_type_tag(poi: Node3D) -> String:
	if poi is PoiInstance:
		var p: PoiInstance = poi as PoiInstance
		match p.poi_type:
			PoiDef.PoiType.OFFENSE:
				return "ATK"
			PoiDef.PoiType.DEFENSE:
				return "DEF"
			PoiDef.PoiType.UTILITY:
				return "UTL"
	return "POI"


func _resolve_docking_manager() -> void:
	if docking_manager_path == NodePath(""):
		return
	_docking_manager = get_node_or_null(docking_manager_path) as DockingManager


func _apply_poi_indicator(ui: Control, poi: Node3D) -> void:
	var indicator: ColorRect = ui.get_node_or_null("HBox/StateIndicator") as ColorRect
	if indicator == null:
		return

	indicator.visible = true
	var poi_instance: PoiInstance = poi as PoiInstance
	var is_visited: bool = _docking_manager != null and poi_instance != null and _docking_manager.is_poi_visited(poi_instance)
	indicator.color = POI_VISITED_COLOR if is_visited else POI_UNVISITED_COLOR


func _reset_poi_indicator(ui: Control) -> void:
	var indicator: ColorRect = ui.get_node_or_null("HBox/StateIndicator") as ColorRect
	if indicator == null:
		return
	indicator.visible = false
	indicator.color = POI_UNVISITED_COLOR

func _apply_gateway_indicator(ui: Control) -> void:
	var indicator: ColorRect = ui.get_node_or_null("HBox/StateIndicator") as ColorRect
	if indicator == null:
		return
	indicator.visible = true
	indicator.color = GATEWAY_COLOR

func _gateway_display_name(gateway: Node3D) -> String:
	if gateway is BossGateway:
		return (gateway as BossGateway).get_display_name()
	return gateway.name
